/*
 *----------------------------------------------------------------------
 *    T-Kernel 2.0 Software Package
 *
 *    Copyright 2011 by Ken Sakamura.
 *    This software is distributed under the latest version of T-License 2.x.
 *----------------------------------------------------------------------
 *
 *    Released by T-Engine Forum(http://www.t-engine.org/) at 2011/05/17.
 *    Modified by TRON Forum(http://www.tron.org/) at 2015/06/01.
 *
 *----------------------------------------------------------------------
 *
 *    Modified by T.Yokobayashi at 2016/03/14.
 *
 *----------------------------------------------------------------------
 */

/*
 *	@(#)memattr.S (monitor/hwdepend) 2016/04/18
 *
 *      manipulation of page table
 */
#define _in_asm_source_

#include <machine.h>
#include <tk/sysdef.h>

#include "setup_rzt1.h"

/*
 * memory barrier macros
 */
.macro _mov reg, val
  .ifnes "\reg", "\val"
	mov	\reg, \val
  .endif
.endm
.macro .ISB reg, val=#0
#ifdef CPU_ARMv6
	_mov	\reg, \val
	mcr	p15, 0, \reg, cr7, c5, 4
#else	/* CPU_ARMv7以降 */
	isb
#endif
.endm
.macro .DSB reg, val=#0
#ifdef CPU_ARMv6
	_mov	\reg, \val
	mcr	p15, 0, \reg, cr7, c10, 4
#else	/* CPU_ARMv7以降 */
	dsb
#endif
.endm
.macro .DMB reg, val=#0
#ifdef CPU_ARMv6
	_mov	\reg, \val
	mcr	p15, 0, \reg, cr7, c10, 5
#else	/* CPU_ARMv7以降 */
	dmb
#endif
.endm

/*
 * change memory attribute
 *	void ChangeMemAttr(UW top, UW end, UW attr)
 *               change the memory attribute of memory area from `top' to `end' - 1 into `attr'
 *               The physical address where T-Monitor resides is assumed to start at 0x00000000.
 *               It must be called with disabled cache.
 */
	.text
	.balign	4
	.globl	Csym(ChangeMemAttr)
	.type	Csym(ChangeMemAttr), %function
Csym(ChangeMemAttr):
changememattr_start:
#ifdef PAGETBL_BASE
	stmfd	sp!, {r4, r5, r7, r9, r10}

        /* Map T-Monitor to 0x00000000 and up, the information before the mapping is saved. */
	ldr	ip, =PAGETBL_BASE
	ldr	r4, [ip]
	ldr	r10, =attr_prev
	str	r4, [r10]
	ldr	r4, =0x00009402		// Strongly-order, Kernel/RO
	str	r4, [ip]

	.DSB	r4
	mcr	p15, 0, r4, cr8, cr7, 0		// I/D TLB invalidate
	mcr	p15, 0, r4, cr7, cr5, 6		// invalidate BTC
	.DSB	r4
	.ISB	r4

        /* Jump to the address into the remapped area */
	ldr	r4, =changememattr_main
	ldr	r9, =0xfff00000
	bic	r4, r4, r9		// r4 &= 0x000fffff
	mov	pc, r4

	.pool

changememattr_main:	
	bic	r2, r2, r9		// r2 &= 0x000fffff
	mov	r4, r0, lsr #20
	add	r4, ip, r4, lsl #2	// r4 = (r2 >> 20) * 4 + PAGETBL_BASE

changememattr_loop:
	ldr	r5, [r4]		// *r4 = (*r4 & 0xfff00000) | r2
	and	r5, r5, r9
	orr	r5, r5, r2
	str	r5, [r4], #4
	add	r0, r0, #0x00100000
	cmp	r0, r1
	bne	changememattr_loop

        /* Jump to the address in the originally mapped area */
	.DSB	r4
	mcr	p15, 0, r4, cr8, cr7, 0		// I/D TLB invalidate
	mcr	p15, 0, r4, cr7, cr5, 6		// invalidate BTC
	.DSB	r4
	.ISB	r4

	ldr	pc, =changememattr_finish

	.pool

changememattr_finish:
        /* Unmap the T-Monitor in the area from 0x00000000 and up. */
	ldr	r4, [r10]			// attr_prev
	str	r4, [ip]			// PAGETBL_BASE

	.DSB	r4
	mcr	p15, 0, r4, cr8, cr7, 0		// I/D TLB invalidate
	mcr	p15, 0, r4, cr7, cr5, 6		// invalidate BTC
	.DSB	r4
	.ISB	r4

	ldmfd	sp!, {r4, r5, r7, r9, r10}
#endif	/* PAGETBL_BASE */
	bx	lr

	.pool


	.bss
attr_prev:
	.long	0


/*----------------------------------------------------------------------
#|History of "memattr.S"
#|======================
#|* 2016/03/02	[app_rzt1]用に、[tef_em1d]用の"memattr.S"を参考に作成。
#|* 2016/04/18	CPU_ARMv7以降の時は、ISB,DSB,DMB命令を使うように修正。
#|* 2016/04/19	PAGETBL_BASEが未定義の時は無効にした
#|
*/
